buildscript {
  dependencies {
    // The output JARs don't include transitive dependencies, so we have
    // to add them manually ourselves :(
    classpath dep.gradleNodePlugin
    classpath dep.androidPlugin
    classpath dep.javaPoet
    classpath dep.kotlinStdLib
    classpath dep.moshi
    classpath dep.pluralizer
    classpath dep.jsr305
    classpath files(project(':apollo-api').jar.outputs.files.getFiles())
    classpath files(project(':apollo-compiler').jar.outputs.files.getFiles())
    classpath files(project(':apollo-gradle-plugin').jar.outputs.files.getFiles())
  }
}

apply plugin: 'com.android.application'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    applicationId "com.example.apollostack.sample"
    minSdkVersion 15
    targetSdkVersion 25
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
    ignore 'InvalidPackage', 'GoogleAppIndexingWarning', 'AllowBackup'
  }
}

dependencies {
  compile dep.appcompat
  compile project(":apollo-runtime")

  testCompile dep.junit
}

apply plugin: 'com.apollostack.android'